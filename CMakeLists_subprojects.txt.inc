GET_FILENAME_COMPONENT(SUBDIRNAME ${CMAKE_CURRENT_SOURCE_DIR} NAME)

IF( DEFINED SPECIFIC_PROJECTNAME )
	# this is for ambix_decoder (which shares the code of ambix_binaural)
	SET (SUBPROJECT_NAME ${SPECIFIC_PROJECTNAME}_o${AMBI_ORDER})
ELSE( DEFINED SPECIFIC_PROJECTNAME )
	# this is the normal way...
	SET (SUBPROJECT_NAME ${SUBDIRNAME}_o${AMBI_ORDER})
ENDIF(DEFINED SPECIFIC_PROJECTNAME )


IF(APPLE)
	IF(WITH_EIGEN)
		ADD_DEFINITIONS(
    		-std=c++98
    		-stdlib=libstdc++
			-Wno-c++11-extensions #avoid c++11 warning msg
			)
	ELSE(WITH_EIGEN)
		# switch to c++11
		ADD_DEFINITIONS(
			-std=c++11
		)
	ENDIF(WITH_EIGEN)
ENDIF (APPLE)

# add the folder with Juce includes
INCLUDE_DIRECTORIES ( JuceLibraryCode )


IF(DEFINED SPECIFIC_SOURE_DIR)
	INCLUDE_DIRECTORIES ( ${SPECIFIC_SOURE_DIR}/JuceLibraryCode )
ENDIF(DEFINED SPECIFIC_SOURE_DIR)

#############################
# add all c, cpp, cc files from the Source directory
FILE ( GLOB_RECURSE SOURCE Source/*.c* )

IF(DEFINED SPECIFIC_SOURE_DIR)
	FILE ( GLOB_RECURSE SOURCE ${SPECIFIC_SOURE_DIR}/Source/*.c* )
	# LIST ( APPEND SOURCE ${SOURCE_2})
ENDIF(DEFINED SPECIFIC_SOURE_DIR)

############################
# ignore some source files (specially made for ambix_decoder)
# little bit strange construction but it works...

IF(DEFINED IGNORE_SRC_FILES)

	FOREACH ( CUR_SRC ${SOURCE} )
		
		GET_FILENAME_COMPONENT(CUR_SRC_NAME ${CUR_SRC} NAME)
		
		LIST ( FIND IGNORE_SRC_FILES ${CUR_SRC_NAME} FOUND )
		IF(NOT ${FOUND} EQUAL -1)
			LIST ( FIND SOURCE ${CUR_SRC} REMOVE_ID )
			LIST ( REMOVE_AT SOURCE ${REMOVE_ID} )
			# MESSAGE ( STATUS "REMOVED SRC FILE: " ${CUR_SRC_NAME})
		ENDIF(NOT ${FOUND} EQUAL -1)
		
	ENDFOREACH ( CUR_SRC ${SOURCE} )
	
ENDIF(DEFINED IGNORE_SRC_FILES)


############################
# add from common directory
IF(WITH_SphericalHarmonic)
	LIST ( APPEND SOURCE ${SRC_DIR}/common/SphericalHarmonic/ShChebyshev.cpp
		${SRC_DIR}/common/SphericalHarmonic/ShLegendre.cpp
		${SRC_DIR}/common/SphericalHarmonic/ShNorm.cpp
		${SRC_DIR}/common/SphericalHarmonic/SphericalHarmonic.cpp)
ENDIF(WITH_SphericalHarmonic)

IF(WITH_SphFilter)
	LIST ( APPEND SOURCE ${SRC_DIR}/common/SphFilter/SphFilter.cpp)
ENDIF(WITH_SphFilter)

IF(WITH_MyMeter)
	LIST ( APPEND SOURCE ${SRC_DIR}/common/MyMeter/MyMeter.cpp)
ENDIF(WITH_MyMeter)

IF(WITH_MyMeterDsp)
	LIST ( APPEND SOURCE ${SRC_DIR}/common/MyMeterDsp/MyMeterDsp.cpp)
ENDIF(WITH_MyMeterDsp)

IF(WITH_T_DESIGN)
	LIST ( APPEND SOURCE ${SRC_DIR}/common/Ressources/t_design.cpp)
ENDIF(WITH_T_DESIGN)

IF(WITH_LegendreU)
	LIST ( APPEND SOURCE ${SRC_DIR}/common/LegendreU/LegendreU.cpp)
ENDIF(WITH_LegendreU)

############################
# AUDIO UNIT SOURCE
IF (BUILD_AU)
	LIST ( APPEND SOURCE ${AU_PLUGIN_SOURCE})
ENDIF (BUILD_AU)

#SORT IT
LIST ( SORT SOURCE )

ADD_LIBRARY(${SUBPROJECT_NAME} MODULE ${JUCE_PLUGIN_CLIENT_SOURCE} ${SOURCE} )

#########
# dependency on JUCE_STATIC_LIB
ADD_DEPENDENCIES (${SUBPROJECT_NAME} ${JUCE_STATIC_LIB})

####################
# additional libs (FFTW3F, liblo, Eigen3 is header only)

IF(WITH_FFTW3)

	# MESSAGE( STATUS "LINKING FFTW3F: " ${FFTW3F_LIBRARY} )
	
	TARGET_LINK_LIBRARIES( ${SUBPROJECT_NAME}
	
		${FFTW3F_LIBRARY}
	)
ENDIF(WITH_FFTW3)

IF(WITH_LIBLO)
	TARGET_LINK_LIBRARIES( ${SUBPROJECT_NAME}
		${LIBLO_LIBRARIES}
	)
ENDIF(WITH_LIBLO)

IF(WITH_LIBSOXR)
	TARGET_LINK_LIBRARIES( ${SUBPROJECT_NAME}
		${LIBSOXR_LIBRARIES}
	)
ENDIF(WITH_LIBSOXR)

IF(WIN32)
  IF (WITH_LIBLO OR WITH_FFTW3)
    TARGET_LINK_LIBRARIES( ${SUBPROJECT_NAME}
		  ${CMAKE_THREAD_LIBS_INIT}
      )
  ENDIF (WITH_LIBLO OR WITH_FFTW3)
ENDIF(WIN32)

####################
#plattform specifics

IF(NOT APPLE)

	#osx need special treating for creating the bundle...
	
	TARGET_LINK_LIBRARIES( ${SUBPROJECT_NAME}
		${JUCE_LIBRARIES}
		${JUCE_STATIC_LIB}
	)
	
	# get the target folder for later copying it
	GET_TARGET_PROPERTY(FILEPATH ${SUBPROJECT_NAME} LOCATION)
	
ENDIF(NOT APPLE)





IF(APPLE)

	#prototype Info.plist
	SET_TARGET_PROPERTIES(${SUBPROJECT_NAME} PROPERTIES MACOSX_BUNDLE_INFO_PLIST ${SRC_DIR}/osx_ressources/MacOSXBundleInfo.plist.in)

	SET_TARGET_PROPERTIES(${SUBPROJECT_NAME} PROPERTIES BUNDLE TRUE)
	SET_TARGET_PROPERTIES(${SUBPROJECT_NAME} PROPERTIES BUNDLE_EXTENSION vst)
	SET_TARGET_PROPERTIES(${SUBPROJECT_NAME} PROPERTIES MACOSX_BUNDLE_BUNDLE_VERSION ${VERSION})
	SET_TARGET_PROPERTIES(${SUBPROJECT_NAME} PROPERTIES MACOSX_BUNDLE_SHORT_VERSION_STRING ${VERSION})
	SET_TARGET_PROPERTIES(${SUBPROJECT_NAME} PROPERTIES MACOSX_BUNDLE_LONG_VERSION_STRING ${VERSION})
	SET_TARGET_PROPERTIES(${SUBPROJECT_NAME} PROPERTIES MACOSX_BUNDLE_BUNDLE_NAME ${SUBPROJECT_NAME})


	SET_TARGET_PROPERTIES(${SUBPROJECT_NAME} PROPERTIES LIBRARY_OUTPUT_DIRECTORY ${BIN_DIR})

	# link libs
	TARGET_LINK_LIBRARIES( ${SUBPROJECT_NAME}
		${JUCE_LIBRARIES}
		${JUCE_STATIC_LIB}
	)
	
	# strip it
	ADD_CUSTOM_COMMAND(
		TARGET ${SUBPROJECT_NAME} POST_BUILD 
		COMMAND ${CMAKE_STRIP} -x -S ${BIN_DIR}/${SUBPROJECT_NAME}.vst/Contents/MacOS/${SUBPROJECT_NAME}
	)
	
	
	#copy whole .vst folder to _bin/vst
	ADD_CUSTOM_COMMAND(
		TARGET ${SUBPROJECT_NAME} POST_BUILD 
		COMMAND ${CMAKE_COMMAND} 
		ARGS -E copy_directory ${BIN_DIR}/${SUBPROJECT_NAME}.vst ${BIN_DIR}/_bin/vst/${SUBPROJECT_NAME}.vst
	)

	#copy PkgInfo
	ADD_CUSTOM_COMMAND(
		TARGET ${SUBPROJECT_NAME} POST_BUILD 
		COMMAND ${CMAKE_COMMAND} 
		ARGS -E copy ${SRC_DIR}/osx_ressources/PkgInfo ${BIN_DIR}/_bin/vst/${SUBPROJECT_NAME}.vst/Contents/PkgInfo
	)
	
	IF( DEFINED OSX_COPY_LIB )
		#copy additional files (eg. dynamic libraries)
		ADD_CUSTOM_COMMAND(
			TARGET ${SUBPROJECT_NAME} POST_BUILD 
			COMMAND ${CMAKE_COMMAND} 
			ARGS -E copy ${SRC_DIR}/mac-libs/${OSX_COPY_LIB} ${BIN_DIR}/_bin/vst/${SUBPROJECT_NAME}.vst/Contents/Frameworks/${OSX_COPY_LIB}
			)
	ENDIF( DEFINED OSX_COPY_LIB )
	
	IF(BUILD_AU)
		#copy whole .vst folder to _bin/au
		ADD_CUSTOM_COMMAND(
			TARGET ${SUBPROJECT_NAME} POST_BUILD 
			COMMAND ${CMAKE_COMMAND} 
			ARGS -E copy_directory ${BIN_DIR}/${SUBPROJECT_NAME}.vst ${BIN_DIR}/_bin/au/${SUBPROJECT_NAME}.component
		)
		#copy PkgInfo
		ADD_CUSTOM_COMMAND(
			TARGET ${SUBPROJECT_NAME} POST_BUILD 
			COMMAND ${CMAKE_COMMAND} 
			ARGS -E copy ${SRC_DIR}/osx_ressources/PkgInfo ${BIN_DIR}/_bin/au/${SUBPROJECT_NAME}.component/Contents/PkgInfo
		)
	ENDIF(BUILD_AU)

	#remove build in home dir
	ADD_CUSTOM_COMMAND(
		TARGET ${SUBPROJECT_NAME} POST_BUILD 
		COMMAND ${CMAKE_COMMAND} 
		ARGS -E remove_directory ${BIN_DIR}/${SUBPROJECT_NAME}.vst
	)

ENDIF(APPLE)


IF(UNIX AND NOT APPLE AND NOT ANDROID)
	IF(BUILD_LV2)
		ADD_CUSTOM_COMMAND(
			TARGET ${SUBPROJECT_NAME} POST_BUILD 
			COMMAND ${CMAKE_COMMAND} 
			ARGS -E copy ${FILEPATH} ${BIN_DIR}/_bin/lv2/${SUBPROJECT_NAME}.lv2/${SUBPROJECT_NAME}.so
		)
	
	ENDIF(BUILD_LV2)
	
	IF(BUILD_VST)
		ADD_CUSTOM_COMMAND(
			TARGET ${SUBPROJECT_NAME} POST_BUILD 
			COMMAND ${CMAKE_COMMAND} 
			ARGS -E copy ${FILEPATH} ${BIN_DIR}/_bin/vst/${SUBPROJECT_NAME}.so
		)
	ENDIF(BUILD_VST)
ENDIF(UNIX AND NOT APPLE AND NOT ANDROID)


IF(WIN32)

	ADD_CUSTOM_COMMAND(
		TARGET ${SUBPROJECT_NAME} POST_BUILD 
		COMMAND ${CMAKE_COMMAND} 
		ARGS -E copy ${FILEPATH} ${BIN_DIR}/_bin/vst/${SUBPROJECT_NAME}.dll
	)
ENDIF(WIN32)

